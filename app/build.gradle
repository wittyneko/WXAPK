apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {

    compileSdkVersion 32

    buildFeatures {
        viewBinding = true
    }

    defaultConfig {
        applicationId "com.twiceyuan.wxapk"
        minSdkVersion 16
        targetSdkVersion 32
        versionCode getAppVersionCode()
        versionName getLastTagName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {

            storeFile rootProject.file("key.keystore")
            storePassword "pa\$\$w0rd"
            keyAlias "k"
            keyPassword "pa\$\$w0rd"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name != "release") return
        outputs.forEach {
            def buildTypeName = variant.buildType.name
            def version = "$versionName($versionCode)"
            it.outputFileName = "WXAPK-${version}-${buildTypeName}.apk"
        }
    }
}

// 根据应用提交记录数来生成版本号
def getAppVersionCode() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'tag', '--list'
        standardOutput = stdout
    }
    // 后面 + 的数字为临时发版没有打 tag 的版本。尽量避免这种情况
    return stdout.toString().split("\n").size() + 1
}

// 获取最新的 tag 名称
def getLastTagName() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--abbrev=0', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

task printVersionInfo {
    doLast {
        println "VersionName: ${getLastTagName()}, VersionCode: ${getAppVersionCode()}"
    }
}

dependencies {
    implementation(fileTree(dir: 'libs', include: ["*.jar"]))
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
}
